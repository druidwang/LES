SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

SET ANSI_PADDING ON
GO

IF EXISTS(SELECT * FROM sys.objects WHERE type='P' AND name='USP_FIND_PUR_PL_4_PUR') 
     DROP PROCEDURE [USP_FIND_PUR_PL_4_PUR] 
GO

CREATE PROCEDURE [dbo].[USP_FIND_PUR_PL_4_PUR] 
(
	@SUPPL_CD VARCHAR(20),
	@EFF_DT DATETIME
) --WITH ENCRYPTION
AS 
BEGIN
	SET NOCOUNT ON

	DECLARE @ERROR_MSG NVARCHAR(MAX)

	IF @SUPPL_CD IS NULL
	BEGIN
		SET @ERROR_MSG = N'供应商代码不能为空。'
		RAISERROR(@ERROR_MSG, 16, 1) 
	END

	IF @EFF_DT IS NULL
	BEGIN
		SET @ERROR_MSG = N'生效日期不能为空。'
		RAISERROR(@ERROR_MSG, 16, 1) 
	END

	CREATE TABLE #TMP_PUR_PL_049 (
		ID INT IDENTITY(1, 1) PRIMARY KEY,
		VOU_LN INT,
		SUB_SEQ INT,  --标记区分一行收货记录取到的多个价格单，最新创建的价格单的SUB_SEQ最小从1开始
		ORD_LN INT,
		DLV_LN INT,
		PUR_PL_ID INT,
		SUPPL_CD VARCHAR(20),
		PT_NO VARCHAR(20),
		QTY DECIMAL(18, 8),
		UOM VARCHAR(3),
		BASE_UOM VARCHAR(3),
		UOM_CONV_RT DECIMAL(18, 8),
		CUR VARCHAR(3),
		UNIT_PRICE DECIMAL(18, 8),
		IS_PROV_EST BIT,
		TAX_CD VARCHAR(5),
		BIL_TERM CHAR(1)
	)

	BEGIN TRY
		IF NOT EXISTS(SELECT TOP 1 1 FROM tempdb.sys.objects WHERE type = 'U' AND name LIKE '#TMP_REC_ORD_INPUT')  --检查是否定义了采购收货输入参数的临时表
		BEGIN
			SET @ERROR_MSG = '没有定义临时表#TMP_REC_ORD_INPUT表。'
			RAISERROR(@ERROR_MSG, 16, 1) 

			--存储过程不会执行到这里
			--为了使用到临时表的地方不显示报错信息
			CREATE TABLE #TMP_REC_ORD_INPUT (
				VOU_LN INT PRIMARY KEY,
				ORD_LN INT,
				DLV_LN INT,
				PT_NO VARCHAR(20),
				UOM VARCHAR(3),
				BASE_UOM VARCHAR(3),
				UOM_CONV_RT DECIMAL(18, 8),
				QTY DECIMAL(18, 8)
			)
		END  

		IF NOT EXISTS(SELECT TOP 1 1 FROM tempdb.sys.objects WHERE type = 'U' AND name LIKE '#TMP_REC_CNMT_INPUT')  --检查是否定义了记录寄售库存的临时表
		BEGIN
			SET @ERROR_MSG = '没有定义临时表#TMP_REC_CNMT_INPUT表。'
			RAISERROR(@ERROR_MSG, 16, 1) 

			--存储过程不会执行到这里
			--为了使用到临时表的地方不显示报错信息
			CREATE TABLE #TMP_REC_CNMT_INPUT (
				VOU_LN INT PRIMARY KEY,
				PUR_PL_ID INT,
				SUPPL_CD VARCHAR(20),
				ORD_LN INT,
				DLV_LN INT,
				PT_NO VARCHAR(20),
				UOM VARCHAR(3),
				BASE_UOM VARCHAR(3),
				UOM_CONV_RT DECIMAL(18, 8),
				CNMT_QTY DECIMAL(18, 8),
				VER INT
			)
		END

		IF NOT EXISTS(SELECT TOP 1 1 FROM tempdb.sys.objects WHERE type = 'U' AND name LIKE '#TMP_REC_BIL_STL_INPUT')  --检查是否定义了结算的临时表
		BEGIN
			SET @ERROR_MSG = '没有定义临时表#TMP_REC_BIL_STL_INPUT表。'
			RAISERROR(@ERROR_MSG, 16, 1) 

			--存储过程不会执行到这里
			--为了使用到临时表的地方不显示报错信息
			CREATE TABLE #TMP_REC_BIL_STL_INPUT (
				VOU_LN INT PRIMARY KEY,
				ORD_LN INT,
				ORD_BOM_ID BIGINT,
				PUR_PL_ID INT,
				PT_NO VARCHAR(20),
				UOM VARCHAR(3),
				BASE_UOM VARCHAR(3),
				UOM_CONV_RT DECIMAL(18, 8),
				STL_QTY DECIMAL(18, 8)
			)
		END

		IF NOT EXISTS(SELECT TOP 1 1 FROM tempdb.sys.objects WHERE type = 'U' AND name LIKE '#TMP_ERR_MSG')  --检查是否定义了错误信息临时表
		BEGIN
			SET @ERROR_MSG = '没有定义临时表#TMP_ERR_MSG表。'
			RAISERROR(@ERROR_MSG, 16, 1) 

			--存储过程不会执行到这里
			--为了使用到临时表的地方不显示报错信息
			CREATE TABLE #TMP_ERR_MSG (
				ID INT IDENTITY(1, 1) PRIMARY KEY,
				LVL CHAR(1),
				MSG NVARCHAR(500)
			)
		END

		--查找价格单
		--根据零件+供应商+采购单位+生产日期查找
		INSERT INTO #TMP_PUR_PL_049 (VOU_LN, SUB_SEQ, ORD_LN, DLV_LN, PUR_PL_ID, SUPPL_CD, 
								PT_NO, QTY, UOM, BASE_UOM, UOM_CONV_RT, CUR, UNIT_PRICE, IS_PROV_EST, TAX_CD, BIL_TERM)
		SELECT A.VOU_LN, ROW_NUMBER() OVER(PARTITION BY B.PUR_PL_ID ORDER BY B.CRT_DT) AS SUB_SEQ, A.ORD_LN, A.DLV_LN, B.PUR_PL_ID, @SUPPL_CD,
		A.PT_NO, A.QTY, A.UOM, A.BASE_UOM, A.UOM_CONV_RT, B.CUR, B.UNIT_PRICE, B.IS_PROV_EST, B.TAX_CD, B.BIL_TERM
		FROM #TMP_REC_ORD_INPUT AS A LEFT JOIN PUR_PL AS B
		ON A.PT_NO = B.PT_NO AND A.UOM = B.UOM
		AND B.SUPPL_CD = @SUPPL_CD
		AND (B.EFF_DT IS NULL OR B.EFF_DT <= @EFF_DT)
		AND (B.EXP_DT IS NULL OR B.EXP_DT > @EFF_DT)

		--同一行收货记录取到的多个价格单，取创建日期最新的一个其余的删除
		DELETE FROM #TMP_PUR_PL_049 WHERE SUB_SEQ > 1

		--没有找到价格单的记录错误信息
		INSERT INTO #TMP_ERR_MSG(LVL, MSG) 
		SELECT 'E', N'没有找到供应商代码为' + @SUPPL_CD + '物料代码为' + PT_NO + '单位为' + UOM + '生效日期为' + CONVERT(VARCHAR(10), @EFF_DT, 120) + '的采购价格单。' 
		FROM #TMP_PUR_PL_049 WHERE PUR_PL_ID IS NULL

		--把寄售物料记录到寄售物料临时表里
		INSERT INTO #TMP_REC_CNMT_INPUT(VOU_LN, PUR_PL_ID, SUPPL_CD, ORD_LN, DLV_LN,
							PT_NO, UOM, BASE_UOM, UOM_CONV_RT, CNMT_QTY)
		SELECT VOU_LN, PUR_PL_ID, SUPPL_CD, ORD_LN, DLV_LN, 
				PT_NO, UOM, BASE_UOM, UOM_CONV_RT, QTY
		FROM #TMP_PUR_PL_049 WHERE BIL_TERM <> 'R'

		--调用准备更新寄售物料余额存储过程
		EXEC USP_PREP_REC_CNMT

		--把需要结算的物料记录到结算临时表里
		INSERT INTO #TMP_REC_BIL_STL_INPUT(VOU_LN, ORD_LN, PUR_PL_ID,
									PT_NO, UOM, BASE_UOM, UOM_CONV_RT, STL_QTY)
		SELECT VOU_LN, ORD_LN, PUR_PL_ID,
				PT_NO, UOM, BASE_UOM, UOM_CONV_RT, QTY
		FROM #TMP_PUR_PL_049 WHERE BIL_TERM = 'R'
	END TRY
	BEGIN CATCH
		SET @ERROR_MSG = N'程序执行时发生错误:' + ERROR_MESSAGE() + N'，行数：' + ERROR_LINE()
		RAISERROR(@ERROR_MSG, 16, 1) 
	END CATCH

	DROP TABLE #TMP_PUR_PL_049
END
GO